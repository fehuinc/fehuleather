angular.module "Shop", []

.controller "ShopCtrl", new Array "$http", "$scope", ($http, $scope)->
  
  ## LOCALS
  showingProduct =
    index: null
    scope: null
  
  
  ## SCOPE LOCALS
  $scope.shopInfos = [
    {name: "Payment", content: "You can pay with your own blood!"}
    {name: "Shipping", content: "We don't actually use ships."}
    {name: "Returns", content: "The return statement lets you get data back out of a function."}
    {name: "Questions", content: "Answers"}
  ]
  
  
  ## LOAD PRODUCT DATA
  
  $http.get("/api/totem").then (response)->
    $scope.products = response.data
    
    for product in $scope.products
      product.specimens = []
      variations = product.variations.sort("level")
      
      # The first variation is what we use for the totem
      firstVariation = variations[0]
      
      # We also want the default variant of all variations that have an image
      # Skip the first variation, because we're already using it for the totem
      namesOfDefaultVariantsWithImages = []
      
      # We're going to loop through all variations and do some setup
      for variation, i in variations
        defaultVariant = null
        defaultVariantIndex = null
        
        for variant, vi in variation.variants when variant.default
          defaultVariant = variant
          defaultVariantIndex = vi
        
        # If there's no default, we'll just use the first result
        unless defaultVariant?
          defaultVariant = variation.variants[0]
          defaultVariantIndex = 0
        
        variation.choice = defaultVariant
        variation.choiceIndex = defaultVariantIndex
        
        if i > 0 and variation.has_image
          namesOfDefaultVariantsWithImages.push defaultVariant.name
          
      
      for variant in firstVariation.variants
        file = "#{product.name}/totem/"
        file += [variant.name].concat(namesOfDefaultVariantsWithImages).join("-")
        file = file.toLowerCase()
            .replace('&', 'and')
            .replace(/[^0-9a-z\-\/]/g, ' ')
            .replace(/\s+/g, '-')
        file = "<%= ENV['IMAGEPATH'] %>#{file}.jpg"
        
        product.specimens.push
          name: variant.name
          totem: file
          description: variant.description
  
  
  ## SCOPE FUNCTIONS
  
  $scope.infoIsOpen = (productIndex)->
    return showingProduct.index is productIndex
  
  $scope.toggleProductInfo = (productIndex, productScope)->
    showingProduct.scope?.showingProductInfo = false
    
    if showingProduct.index is productIndex
      showingProduct.index = null
      showingProduct.scope = null
    else
      showingProduct.index = productIndex
      showingProduct.scope = productScope
      showingProduct.scope?.showingProductInfo = true
  
