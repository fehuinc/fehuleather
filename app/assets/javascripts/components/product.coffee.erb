angular.module "Product", []

.directive "product", ()->
  templateUrl: "<%= asset_path('components/product.html') %>"
  
  controller: new Array "$scope", ($scope)->
    
    ## LOCALS
    productIndex = $scope.$index
    
    
    ## SCOPE LOCALS
    $scope.variation = $scope.product.specimens[0]
    
    
    ## HELPERS
    getVariation = (variationIndex)->
      specimens = $scope.product.specimens
      return specimens[(variationIndex + specimens.length) % specimens.length]
    
    
    ## SCOPE
    $scope.variationsCount = ()->
      return $scope.product.specimens.length
    
    $scope.getTotemImage = (variationIndex)->
      return getVariation(variationIndex)?.totem
    
    $scope.getVerticalPositionStyle = ()->
      ypos = if $scope.infoIsOpen(productIndex) then $scope.product.ypos else 0
      translate = "translate(-50%, -#{ypos}%)"
      return style =
        transform: translate
        "-webkit-transform": translate
    
    $scope.changeVariation = (variationIndex)->
      $scope.variation = getVariation(variationIndex)
